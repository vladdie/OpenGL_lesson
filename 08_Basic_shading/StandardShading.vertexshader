#version 400 core

//Input vertex data, different for all executions of this shader.
layout (location = 0) in vec3 vertexPosition_modelspace;
layout (location = 1) in vec2 vertexUV;
layout (location = 2) in vec3 vertexNormal_modelspace;

//Output data ; will be interpolated for each fragment.
out vec2 UV;						//纹理坐标
out vec3 Position_worldspace;		//世界坐标系中顶点坐标
out vec3 Normal_cameraspace;		//相机坐标系中顶点法向量
out vec3 EyeDirection_cameraspace;	//相机坐标系中视线方向(朝向相机),其实应该是从相机看向顶点
out vec3 LightDirection_cameraspace;//相机坐标系中光的方向

//Values that stay constant for the whole mesh.
uniform mat4 MVP;	//MVP矩阵
uniform mat4 V;		//View矩阵
uniform mat4 M;		//Model矩阵
uniform vec3 LightPosition_worldspace;

void main()
{
	//Output position of the vertex, in clip space : MVP * position
	gl_Position = MVP * vec4(vertexPosition_modelspace, 1);

	//Position of the vertex, in worldspace : M * position
	Position_worldspace = (M * vec4(vertexPosition_modelspace, 1)).xyz;

	//Vector that goes from the vertex to the camera, in camera space.
	//In camera space, the camera is at the origin (0,0,0).
	vec3 vertexPosition_cameraspace = (V * M * vec4(vertexPosition_modelspace, 1)).xyz;
	EyeDirection_cameraspace = vec3(0,0,0) - vertexPosition_cameraspace;

	//Vector that goes from the vertex to the night, in camera space. M is ommited
	//because it's identity
	vec3 LightPosition_cameraspace = (V * vec4(LightPosition_worldspace, 1)).xyz;
	LightDirection_cameraspace = LightPosition_cameraspace + EyeDirection_cameraspace;

	//Normal of the vertex, in camera space
	//Only correct if ModelMatrix does not scale the model! Use its inverse transpose if not.
	Normal_cameraspace = ( V * M * vec4(vertexNormal_modelspace, 0)).xyz;

	//UV of the vertex. No special space for this one.
	UV = vertexUV;
}