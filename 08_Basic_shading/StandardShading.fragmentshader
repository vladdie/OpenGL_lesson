#version 400 core

//Interpolated values from the vertex shaders
in vec2 UV;						//从顶点着色器接收的顶点纹理
in vec3 Position_worldspace;
in vec3 Normal_cameraspace;
in vec3 EyeDirection_cameraspace;
in vec3 LightDirection_cameraspace;

//Output data
out vec3 color;

//Values that stay constant for the whole mesh
uniform sampler2D myTextureSampler;
uniform mat4 MV;
uniform vec3 LightPosition_worldspace;

void main()
{
	//Light emission properties
	//You probably want to put them as uniforms
	vec3 LightColor = vec3(1,1,1);
	float LightPower = 50.0f;

	//Material properties
	vec3 MaterialDiffuseColor = texture2D( myTextureSampler, UV).rgb;	//材料漫反射颜色
	vec3 MaterialAmbientColor = vec3(0.1,0.1,0.1)*MaterialDiffuseColor;	//材料环境颜色
	vec3 MaterialSpecularColor = vec3(0.3,0.3,0.3);						//材料镜面反射颜色

	//Distance to the light
	float distance = length( LightPosition_worldspace - Position_worldspace );

	//Normal of the computed fragment, in camera space
	vec3 n = normalize( Normal_cameraspace );
	//Direction of the light (from the fragment to the light)
	vec3 l = normalize( LightDirection_cameraspace );
	//Cosine of the angle between the normal and the light direction,
	//clamped above 0
	// - light is at the vertical of the triangle ->1
	// - light is perpendicular to the triangle ->0
	// - light is behind the triangle ->0
	float cosTheta = clamp( dot(n,l), 0, 1);

	//Eye vector(towards the camera)
	vec3 E = normalize(EyeDirection_cameraspace);
	//Direction in which the triangle reflects the light
	vec3 R = reflect(-l,n);
	//R是反射光的方向，E是视线的反方向（就像之前对“l”的假设）；如果二者夹角很小，意味着视线与反射光线重合。
	//pow(cosAlpha,5)用来控制镜面反射的波瓣。可以增大5来获得更大的波瓣。

	//Cosine of the angle between the Eye vector and the Reflect vector,
	//clamped to 0
	// - Looking into the reflection ->1
	// - Looking elsewhere -> <1
	float cosAlpha = clamp( dot(E, R), 0, 1);

	color =
		//Ambient : simulates indirect lighting
		MaterialAmbientColor +
		//Diffuse : "color" of the object
		MaterialDiffuseColor * LightColor * LightPower * cosTheta / (distance*distance) +
		//Specular : reflective highlight,like a mirror
		MaterialSpecularColor * LightColor * LightPower * pow(cosAlpha, 5) / (distance*distance);

}
